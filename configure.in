#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.61)
AC_INIT([calf],[0.0.6],[wdev@foltman.com])
AC_CONFIG_SRCDIR([config.h.in])
AC_CONFIG_HEADER([config.h])

AM_INIT_AUTOMAKE(1.8)

if test "x$prefix" = "xNONE"; then 
  prefix=$ac_default_prefix
fi

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LIBTOOL

# Checks for libraries.
PKG_PROG_PKG_CONFIG

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([memory.h stdint.h stdlib.h time.h math.h])

AC_CHECK_HEADER(ladspa.h, LADSPA_FOUND="yes", LADSPA_FOUND="no")

AC_CHECK_HEADER(dssi.h, DSSI_FOUND="yes", DSSI_FOUND="no")

AC_CHECK_HEADERS([jack/jack.h], have_jack_header=yes, have_jack_header=no)

if test "$have_jack_header" = "yes"; then
  PKG_CHECK_MODULES(JACK_DEPS, jack >= 0.103.0,
    AC_CHECK_LIB([jack], [jack_port_new], JACK_FOUND="yes", JACK_FOUND="no"),
    JACK_FOUND="no")
else
  JACK_FOUND="no"
fi

if test "$JACK_FOUND" = "yes"; then
  PKG_CHECK_MODULES(JACK_MIDI_DEPS, jack >= 0.105.0, OLD_JACK="no", OLD_JACK="yes")
fi

CXXFLAGS="$CXXFLAGS -O3 -finline-functions -finline-functions-called-once -Wall"

AC_SUBST(GUI_DEPS_CFLAGS)
AC_SUBST(GUI_DEPS_LIBS)
AC_SUBST(JACK_DEPS_CFLAGS)
AC_SUBST(JACK_DEPS_LIBS)

PHAT_ENABLED="no"
if test "$JACK_FOUND" = "yes"; then
  PKG_CHECK_MODULES(GUI_DEPS, gtk+-2.0 >= 2.4.0, 
    JACK_ENABLED="yes",
    JACK_ENABLED="no (GTK+ is missing or too old)"
  )
else
  JACK_ENABLED="no"
fi

if test "$JACK_ENABLED" = "yes"; then
  PKG_CHECK_MODULES(PHAT_DEPS, phat >= 0.4.1, 
    [
      CFLAGS="$CFLAGS $GUI_DEPS_CFLAGS $JACK_DEPS_CFLAGS"
      CPPFLAGS="$CPPFLAGS $GUI_DEPS_CFLAGS $JACK_DEPS_CFLAGS"
      AC_CHECK_HEADERS([phat/phatknob.h], 
        PHAT_ENABLED="yes", 
        PHAT_ENABLED="no (phat library is too old, SVN version required)")
    ],
    PHAT_ENABLED="no"
  )
fi

AC_MSG_CHECKING([whether to enable experimental/unfinished features])
AC_ARG_ENABLE(experimental,
  AC_HELP_STRING([--enable-experimental],[enable unfinished features - not recommended!]),
  [set_enable_experimental="$enableval"],
  [set_enable_experimental="no"])

AC_MSG_RESULT($set_enable_experimental)

AM_CONDITIONAL(USE_DSSI, test "$DSSI_FOUND" = "yes")
AM_CONDITIONAL(USE_LADSPA, test "$LADSPA_FOUND" = "yes")
AM_CONDITIONAL(USE_JACK, test "$JACK_ENABLED" = "yes")
AM_CONDITIONAL(USE_PHAT, test "$PHAT_ENABLED" = "yes")
AM_CONDITIONAL(ENABLE_EXPERIMENTAL, test "$set_enable_experimental" = "yes")
AM_CONDITIONAL(OLD_JACK, test "$OLD_JACK" = "yes")

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_INT16_T
AC_TYPE_INT32_T
AC_TYPE_INT64_T
AC_TYPE_INT8_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T
AC_TYPE_UINT64_T
AC_TYPE_UINT8_T

AC_MSG_CHECKING(where to install LADSPA plugins)
AC_ARG_WITH(ladspa_dir,
  AC_HELP_STRING([--with-ladspa-dir],[install LADSPA plugins to DIR (default=$prefix/lib/ladspa/)]),
  ,
  [with_ladspa_dir="$prefix/lib/ladspa/"])
AC_MSG_RESULT($with_ladspa_dir)
AC_SUBST(with_ladspa_dir)

AC_MSG_CHECKING(where to install LADSPA RDF file)
AC_ARG_WITH(ladspa_rdf_dir,
  AC_HELP_STRING([--with-ladspa-rdf-dir],[install RDF file to DIR (default=$prefix/share/ladspa/rdf/)]),
  ,
  [with_ladspa_rdf_dir="$prefix/share/ladspa/rdf/"])
AC_MSG_RESULT($with_ladspa_rdf_dir)
AC_SUBST(with_ladspa_rdf_dir)

AC_MSG_CHECKING(where to install DSSI plugins)
AC_ARG_WITH(dssi_dir,
  AC_HELP_STRING([--with-dssi-dir],[install DSSI plugins to DIR (default=$prefix/lib/dssi/)]),
  ,
  [with_dssi_dir="$prefix/lib/dssi/"])
AC_MSG_RESULT($with_dssi_dir)
AC_SUBST(with_dssi_dir)

# Checks for library functions.
AC_CHECK_FUNCS([floor memset pow])

AC_CONFIG_FILES([Makefile
                 src/Makefile
                 src/calf/Makefile])
AC_MSG_RESULT([
    Calf configured

    LADSPA enabled:         $LADSPA_FOUND
    DSSI enabled:           $DSSI_FOUND
    JACK host enabled:      $JACK_ENABLED
    Old-style JACK MIDI:    $OLD_JACK
    PHAT library enabled:   $PHAT_ENABLED
    
    Installation prefix:    $prefix
])
AC_OUTPUT
